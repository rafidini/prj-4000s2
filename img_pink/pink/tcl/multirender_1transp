# Copyright ESIEE (2009) 
#
# m.couprie@esiee.fr
#
# This software is an image processing library whose purpose is to be
# used primarily for research and teaching.
#
# This software is governed by the CeCILL  license under French law and
# abiding by the rules of distribution of free software. You can  use, 
# modify and/ or redistribute the software under the terms of the CeCILL
# license as circulated by CEA, CNRS and INRIA at the following URL
# "http://www.cecill.info". 
#
# As a counterpart to the access to the source code and  rights to copy,
# modify and redistribute granted by the license, users are provided only
# with a limited warranty  and the software's author,  the holder of the
# economic rights,  and the successive licensors  have only  limited
# liability. 
#
# In this respect, the user's attention is drawn to the risks associated
# with loading,  using,  modifying and/or developing or reproducing the
# software by the user in light of its specific status of free software,
# that may mean  that it is complicated to manipulate,  and  that  also
# therefore means  that it is reserved for developers  and  experienced
# professionals having in-depth computer knowledge. Users are therefore
# encouraged to load and test the software's suitability as regards their
# requirements in conditions enabling the security of their systems and/or 
# data to be ensured and,  more generally, to use and operate it in the 
# same conditions as regards security. 
#
# The fact that you are presently reading this means that you have had
# knowledge of the CeCILL license and that you accept its terms.
#
#sh
# The next line is executed by /bin/sh, but not Tcl \
exec vtk $0 $1 $2 $3 $4 $5 $6 $7

# A multiple renderer - Michel Couprie 2006
package require vtk
package require vtkinteraction

# set window title
wm title . "Multiple Renderer"

# get input image file name as first argument
if {($argc < 1) || ($argc > 8)} { 
  puts stderr "usage: render in1 \[in2 ... in8\]"
  exit 
}

set PINK "$env(PINK)"
source "$PINK/tcl/my_write.tcl"
source "$PINK/tcl/my_read.tcl"
source "$PINK/tcl/my_dialog.tcl"

# ============================================================
# GLOBAL VARIABLES
# ============================================================

 global RENDER
 # infilename  : input file name
 # rs : row size
 # cs : col size
 set RENDER(name) "render"
 set RENDER(rs) 600
 set RENDER(cs) 600
 set RENDER(active) 0
 set RENDER(opacity) 1.0
 set RENDER(wireframe) 0
 set RENDER(indexframe) 0
 set RENDER(moviename) "render.avi"
 set RENDER(moviereverse) 1
 set RENDER(movielength) 30
 set RENDER(fps) 15

# white
 set RENDER_COL(0) "1.0 1.0 1.0"
# orange
 set RENDER_COL(1) "1.0 0.49 0.25"
# indigo
 set RENDER_COL(2) "0.49 0.25 1.0"
# red
 set RENDER_COL(3) "1.0 0.0 0.0"
# green
 set RENDER_COL(4) "0.49 1.0 0.25"
#
 set RENDER_COL(5) "1.0 0.25 0.49"
#
 set RENDER_COL(6) "0.25 0.49 1.0"
# black
 set RENDER_COL(7) "0.1 0.1 0.1"

 set RENDER_OPA(0) 1.0
 set RENDER_OPA(1) 1.0
 set RENDER_OPA(2) 1.0
 set RENDER_OPA(3) 1.0
 set RENDER_OPA(4) 1.0
 set RENDER_OPA(5) 1.0
 set RENDER_OPA(6) 1.0
 set RENDER_OPA(7) 1.0
 set RENDER_WFR(0) 0
 set RENDER_WFR(1) 0
 set RENDER_WFR(2) 0
 set RENDER_WFR(3) 0
 set RENDER_WFR(4) 0
 set RENDER_WFR(5) 0
 set RENDER_WFR(6) 0
 set RENDER_WFR(7) 0

# create renderer and window
vtkRenderer ren1
    ren1 SetViewport 0 0 1 1
# fond gris clair:
    ren1 SetBackground 0.8 0.8 0.8
# pour un gradient: nouvelle version VTK ?
#    ren1 SetBackground2 1 1 1
# fond blanc:
#    ren1 SetBackground 1 1 1
# fond bleu:
#    ren1 SetBackground 0 0 0.7
vtkRenderWindow renWin
    renWin AddRenderer ren1 
vtkRenderWindowInteractor iren
    iren SetRenderWindow renWin
vtkInteractorStyleTrackballCamera style
    iren SetInteractorStyle style

# CHARGEMENT DES DIFFERENTES GEOMETRIES
for {set i 0} {$i < $argc} {incr i} {
  vtkPolyDataReader obj$i
  obj$i SetFileName [lindex $argv $i]
  vtkPolyDataNormals normals$i
  normals$i SetInput [obj$i GetOutput]
  normals$i FlipNormalsOn
  vtkPolyDataMapper objMapper$i
  objMapper$i SetInput [normals$i GetOutput]
  vtkActor objActor$i
  objActor$i SetMapper objMapper$i
  eval [objActor$i GetProperty] SetColor $RENDER_COL($i)
  ren1 AddProp objActor$i
}

set cam1 [ren1 GetActiveCamera]
$cam1 ParallelProjectionOff

ren1 ResetCamera

# then binds rendering window to a tk widget
set vtkw [vtkTkRenderWidget .ren \
        -width $RENDER(rs) \
        -height $RENDER(cs) \
        -rw renWin]
BindTkRenderWidget $vtkw

# command zone
frame .commands
frame .buttons
set quitbutton [button .buttons.quit -text "Quit" -command RENDER_bye\
                -activebackground white]
set savebutton [button .buttons.save -text "Save" -command RENDER_savepng\
                -activebackground white]
set moviebutton [button .buttons.movie -text "Movie" -command RENDER_movie\
                -activebackground white]
# what to do in case of window closing:
wm protocol . WM_DELETE_WINDOW RENDER_bye
bind . "q" RENDER_bye

set wirebutton [checkbutton .commands.wire -text "Wireframe" \
                -variable RENDER(wireframe) -command RENDER_togglewire\
                -activebackground white]
set switchbutton [button .commands.switchb -text "Switch Object:" -command RENDER_switch\
                -activebackground white]
set switchlabel [label .commands.switchl -textvariable RENDER(active) -width 5]
set opacity [scale .commands.opacity \
        -from 0.0 -to 1.0 -res 0.05 \
        -orient horizontal \
        -label "Opacity" \
	-variable RENDER(opacity) \
        -command setOpacity]

proc setOpacity {opa} {
  global RENDER
  global RENDER_OPA
  set a $RENDER(active)
  global objActor$a
  eval [objActor$a GetProperty] SetOpacity $opa
  renWin Render
  set RENDER(opacity) $opa
  set RENDER_OPA($a) $opa
}
# set transparency for object 0
setOpacity 0.20

set saveposbutton [button .buttons.savepos -text "Savepos" -command RENDER_savepos\
                -activebackground white]
set setposbutton [button .buttons.setpos -text "SetPos" -command RENDER_setpos\
                -activebackground white]

pack $quitbutton -side right
pack $savebutton -side left
pack $moviebutton -side left
pack $saveposbutton -side left
pack $setposbutton -side left
pack $switchbutton -side left
pack $switchlabel -side left
pack $wirebutton -side left
pack $opacity .buttons -side top -anchor nw -fill both
pack $vtkw .commands -side top -fill both -expand yes

proc RENDER_switch {} {
  global RENDER
  global RENDER_OPA
  global RENDER_WFR
  global argc
  set a [expr $RENDER(active) + 1]
  if {$a == $argc} { set a 0 }
  set RENDER(active) $a
  set RENDER(opacity) $RENDER_OPA($a)
  set RENDER(wireframe) $RENDER_WFR($a)
}

proc RENDER_savepos {} {
  global ren1
  set pos [[ren1 GetActiveCamera] GetPosition]
  set foc [[ren1 GetActiveCamera] GetFocalPoint]
  set vup [[ren1 GetActiveCamera] GetViewUp]
  set sca [[ren1 GetActiveCamera] GetParallelScale]
  my_write_text "_multirender_coord_camera" "$pos\n$foc\n$vup\n$sca"
}

proc RENDER_setpos {} {
  global ren1
  global renWin
  set pos [my_read_text "_multirender_coord_camera"]
  [ren1 GetActiveCamera] SetPosition [lindex $pos 0] [lindex $pos 1] [lindex $pos 2]
  [ren1 GetActiveCamera] SetFocalPoint [lindex $pos 3] [lindex $pos 4] [lindex $pos 5]
  [ren1 GetActiveCamera] SetViewUp [lindex $pos 6] [lindex $pos 7] [lindex $pos 8]
  [ren1 GetActiveCamera] SetParallelScale [lindex $pos 9]
  renWin Render
}

proc RENDER_bye {} {
    vtkCommand DeleteAllObjects
    exit
}

proc RENDER_togglewire {} {
  global RENDER
  global RENDER_WFR
  set a $RENDER(active)
  global objActor$a
  if ($RENDER(wireframe)) {
    eval [objActor$a GetProperty] SetRepresentationToWireframe
  } else {
    eval [objActor$a GetProperty] SetRepresentationToSurface
  }
  renWin Render
  set RENDER_WFR($a) $RENDER(wireframe)
}

proc RENDER_savepng {} {
  global RENDER
  set i $RENDER(indexframe)
  set ii [format "%04d" $i]
  vtkWindowToImageFilter w2i$i
  w2i$i SetInput renWin

  vtkPNGWriter writer$i
  writer$i SetInput [w2i$i GetOutput]
  writer$i SetFileName "render_save$ii.png"
  writer$i Write
  set RENDER(indexframe) [expr $i+1]
}

proc RENDER_movie {} {
 global ren1
 global RENDER
 set RENDER(movielength) [Dialog_Prompt "Movie length" $RENDER(movielength)]
 set RENDER(fps) [Dialog_Prompt "Frames per sec" $RENDER(fps)]
 set RENDER(moviename) [Dialog_Prompt "Movie filename" $RENDER(moviename)]
 for {set i 0} {$i < $RENDER(movielength)} {incr i} {
   # render the image
   renWin Render
   # rotate the active camera by one degree
   [ren1 GetActiveCamera] Azimuth 0.2
#   [ren1 GetActiveCamera] Roll 1
   RENDER_savepng
 }
 if ($RENDER(moviereverse)) {
  RENDER_savepng
  RENDER_savepng
  RENDER_savepng
  for {set i 0} {$i < $RENDER(movielength)} {incr i} {
   # render the image
   renWin Render
   # rotate the active camera by one degree
   [ren1 GetActiveCamera] Azimuth -0.2
#   [ren1 GetActiveCamera] Roll 1
   RENDER_savepng
  }
  RENDER_savepng
  RENDER_savepng
  RENDER_savepng
 }
 catch { exec png2avi "render_save" $RENDER(rs) $RENDER(cs) $RENDER(fps) $RENDER(moviename) } mes
 foreach file [glob -nocomplain "render_save*.png"] {
   exec rm $file
 }
}
